plugins {
    id 'java'
    id 'groovy'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src', 'vars']
        }
    }
    test {
        groovy {
            srcDirs = ['test']
        }
    }
}

sourceCompatibility = 1.8

project.buildDir = 'target'

repositories {
    mavenCentral()
    maven { url "https://repo.jenkins-ci.org/releases" }
    maven { url "https://repo.jenkins-ci.org/public" }
}

dependencies {
    def withoutIcu = { exclude group: 'com.ibm.icu', module: 'icu4j' }

    // mandatory dependencies for using Spock
    // note: please use same Groovy version as on Jenkins
    implementation "org.codehaus.groovy:groovy-all:2.4.12"
    implementation "com.cloudbees:groovy-cps:1.31@jar", withoutIcu
    implementation "org.jenkins-ci.main:jenkins-core:2.225", withoutIcu
    implementation "org.jenkins-ci.plugins:badge:1.8@jar"
    implementation "org.jenkins-ci.plugins:pipeline-maven:3.9.3@jar" // dependency for MavenReport
    implementation "org.jenkins-ci.plugins:credentials-binding:1.27.1@jar"

    implementation "org.slf4j:jcl-over-slf4j:1.7.25"
    testImplementation "org.slf4j:log4j-over-slf4j:1.7.25"
    testImplementation "org.slf4j:slf4j-api:1.7.25"

    testImplementation "ch.qos.logback:logback-core:1.2.3"
    testImplementation "ch.qos.logback:logback-classic:1.2.3"
    testImplementation "com.google.guava:guava:20.0"

    testImplementation "org.spockframework:spock-core:1.3-groovy-2.4@jar"

    // Jenkins Pipeline Unit + JUnit 4
    testImplementation "com.lesfurets:jenkins-pipeline-unit:1.13"
    testImplementation "org.assertj:assertj-core:3.22.0"

    // Jenkins related
    testImplementation "com.homeaway.devtools.jenkins:jenkins-spock:2.1.4"
    testImplementation "javax.servlet:javax.servlet-api:3.1.0"
    testImplementation "org.jenkins-ci.main:jenkins-core:2.225", withoutIcu
    testImplementation "org.jenkins-ci.plugins.workflow:workflow-api:2.40@jar"
    testImplementation "org.jenkins-ci.plugins.workflow:workflow-step-api:2.22@jar"
    testImplementation "org.jenkins-ci.plugins.workflow:workflow-cps:2.78@jar"
    testImplementation "org.jenkins-ci.plugins.workflow:workflow-durable-task-step:2.35@jar" // provides the sh() pipeline step
    testImplementation "org.jenkins-ci.plugins.workflow:workflow-basic-steps:2.20@jar" // provides the echo(), withEnv(), dir() pipeline step
    testImplementation "org.jenkins-ci.plugins:durable-task:1.33@jar" // transitive dependency for workflow-durable-task-step
    testImplementation "org.jenkins-ci.plugins:pipeline-stage-step:2.3@jar" // provides stage() step
    testImplementation "org.jenkins-ci.plugins.workflow:workflow-scm-step:2.11@jar" // provides checkout()
    testImplementation "org.jenkins-ci.plugins:credentials-binding:1.20@jar" // provides withCredentials() step
    testImplementation "org.jenkins-ci.plugins:script-security:1.68@jar"
    testImplementation "com.openshift.jenkins.plugins:openshift-client:1.0.35@jar"
    testImplementation "org.jenkins-ci.plugins:ansicolor:0.5.2@jar"
}

// this is needed for spock to find all the source code in the var directory
task copyGlobalLibVars (type: Copy) {
    from "$rootDir/vars"
    include '**/*.groovy'
    into "$buildDir/classes/vars"
}

compileTestGroovy {
    options.incremental = true
    options.fork        = true
    options.failOnError = false
}
compileTestGroovy.dependsOn copyGlobalLibVars

task printClasspath {
    doLast {
        configurations.testRuntimeClasspath.each { println it }
    }
}
